[!]=============================================================[/!]
[!] 				  File Utility Functions					[/!]
[!] 						 8/21/15							[/!]
[!] 							-								[/!]
[!] 			(When we need to access file info...)			[/!]
[!]=============================================================[/!]

[!]======================================[/!]
[!]	This function checks to see if a 	 [/!]
[!]	path resolves to a Folder or not.	 [/!]
[!]	Equals T if it is a Folder,			 [/!]
[!]	F otherwise. 						 [/!]
[!]======================================[/!]
[Function name=OCB_isFolder]
	[Fileinfo file=[path]]
		[Showif [isFolder]=T]
			[RETURN]T[/RETURN]
		[/Showif]
		[Showif [isFolder]=F]
			[RETURN]F[/RETURN]
		[/Showif]
	[/Fileinfo]
[/Function]

[!]======================================[/!]
[!]	This function checks to see if a 	 [/!]
[!]	file or folder exists.				 [/!]
[!]	Equals T if it exists,				 [/!]
[!]	F otherwise. 						 [/!]
[!]======================================[/!]
[Function name=OCB_fExists]
	[Fileinfo file=[path]]
		[Showif [exists]=T]
			[RETURN]T[/RETURN]
		[/Showif]
		[Showif [exists]=F]
			[RETURN]F[/RETURN]
		[/Showif]
	[/Fileinfo]
[/Function]

[!]======================================[/!]
[!]	This function checks to see if a 	 [/!]
[!]	path resolves to a file or not. 	 [/!]
[!]	Equals T if it is a file,			 [/!]
[!]	F otherwise. 						 [/!]
[!]======================================[/!]
[Function name=OCB_isFile]
	[Fileinfo file=[path]]
		[Showif [isFile]=T]
			[RETURN]T[/RETURN]
		[/Showif]
		[Showif [isFile]=F]
			[RETURN]F[/RETURN]
		[/Showif]
	[/Fileinfo]
[/Function]

[!]======================================[/!]
[!]	This function checks to see if a 	 [/!]
[!]	Folder exists and creates it if 	 [/!]
[!]	it does not.						 [/!]
[!]======================================[/!]
[Function name=OCB_fCheck]
	[Showif [OCB_fExists path=[path]]=F]
		[Createfolder path=[path]]
	[/Showif]
	[RETURN][/RETURN]
[/Function]

[!]======================================[/!]
[!]	This function is a modified version	 [/!]
[!]	of fCheck that returns T if the  	 [/!]
[!]	folder was created (did not exist),  [/!]
[!]	F if not created (already exists). 	 [/!]
[!]	F being returned does not indicate 	 [/!]
[!]	a failure to create the folder. 	 [/!]
[!]======================================[/!]
[Function name=OCB_fCheckWRet]
	[If "[OCB_fExists path=[path]]"="F"]
		[Then]
			[RETURN]T[/RETURN]
			[Createfolder path=[path]]
		[/Then]
		[Else]
			[RETURN]F[/RETURN]
		[/Else]
	[/If]
[/Function]

[!]======================================[/!]
[!]	This function gets the name of  	 [/!]
[!]	a file from a file with a path 		 [/!]
[!]======================================[/!]
[Function name=OCB_fName]
	[Fileinfo file=[file]]
		[RETURN][Filename][/RETURN]
	[/Fileinfo]
[/Function]

[!]======================================[/!]
[!]	This function gets the fullpath to	 [/!]
[!]	a file or folder  		 			 [/!]
[!]======================================[/!]
[Function name=OCB_fPath]
	[FileInfo file=[file]]
		[RETURN][FullPath][/RETURN]
	[/FileInfo]
[/Function]

[!]======================================[/!]
[!]	This function gets the modification	 [/!]
[!]	date for this file or folder  		 [/!]
[!]======================================[/!]
[Function name=OCB_fModDate]
	[FileInfo file=[file]]
		[RETURN][ModDate][/RETURN]
	[/FileInfo]
[/Function]

[!]======================================[/!]
[!]	This function gets the modification	 [/!]
[!]	time for this file or folder  		 [/!]
[!]======================================[/!]
[Function name=OCB_fModTime]
	[FileInfo file=[file]]
		[RETURN][ModTime][/RETURN]
	[/FileInfo]
[/Function]

[!]======================================[/!]
[!]	This function gets the mod date and	 [/!]
[!]	time for this file or folder		 [/!]
[!]======================================[/!]
[Function name=OCB_fModDateTime]
	[FileInfo file=[file]]
		[RETURN][ModDate] [ModTime][/RETURN]
	[/FileInfo]
[/Function]

[!]======================================[/!]
[!]	This function gets the size of the	 [/!]
[!]	file, 'returns' 0 if folder	 		 [/!]
[!]======================================[/!]
[Function name=OCB_fileSize]
	[FileInfo file=[file]]
		[RETURN][Size][/RETURN]
	[/FileInfo]
[/Function]

[!]======================================[/!]
[!]	This function will copy folders to	 [/!]
[!]	another location. This replaces		 [/!]
[!]	the standard COPYFOLDER tag as it	 [/!]
[!]	does not work on the OCB server.	 [/!]
[!]	This tag will work recursivly to 	 [/!]
[!]	pull all subfolders and files to	 [/!]
[!]	the new location. It will generate	 [/!]
[!]	an error if the 'From' location is	 [/!]
[!]	not found, and will otherwise		 [/!]
[!]	ignore the tag.						 [/!]
[!]======================================[/!]
[Function name=OCB_CopyFolder]
	[!] Check If Path Exists [/!]
	[OCB_fCheck path=[to]]
	[Fileinfo file=[from]]
		[Showif [exists]=T]
			[Listfiles path=[from]]
				[Hideif [filename]=Thumbs.db]
					[Showif [isFile]=T]
						[CopyFile from=[from]/[filename]&to=[to]/[filename]]
					[/Showif]
					[Hideif [isFile]=T]
						[OCB_CopyFolder from=[from]/[filename]&to=[to]/[fileName]]
					[/Hideif]
				[/Hideif]
			[/Listfiles]
		[/Showif]
		[Hideif [exists]=T]
			[RETURN]Path '[from]' Does Not Exist, Ignoring OCB_CopyFolder Context[/RETURN]
		[/Hideif]
	[/Fileinfo]
	[RETURN][/RETURN]
[/Function]