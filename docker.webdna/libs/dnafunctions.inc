[!]=============================================================[/!]
[!] 					WebDNA Functions						[/!]
[!] 						9/17/2015							[/!]
[!] 							-								[/!]
[!] 			(Things we do over and over again...)			[/!]
[!]=============================================================[/!]

[!]=========================================================[/!]
[!]															[/!]
[!]					DNA Internal Functions					[/!]
[!]						  9/17/2015							[/!]
[!]	 	These functions are meant to be used with the 		[/!]
[!]	  below functions ONLY and may not work on their own!	[/!]
[!]							-MB								[/!]
[!]=========================================================[/!]
[!] Returns the index portion of the line [/!]
[Function name=i_printV_formatIndex]
	[Switch value=[pIndexType]]
		[Case value=T]
			[return][ipIndex]: [/return]
		[/Case]
		[Default]
			[return][/return]
		[/Default]
	[/Switch]
[/Function]

[!] Builds the Line for the variables [/!]
[Function name=i_build_line]
	[Return][i_printV_formatIndex pIndexType=[pFormatIndex]&ipIndex=[pIndex]][/return]
	[Return]"[pName]" - "[pValue]"[/Return]
[/Function]

[!]=============================================================[/!]
[!]	Prints the Formvariables to the page.						[/!]
[!] Parameters: 												[/!]
[!] 		formatIndex - display the Index: T/F (Optional)   	[/!]
[!] 							Defaults to T				  	[/!]
[!] 		oneLine 	- display on One Line: T/F (Optional) 	[/!]
[!] 							Defaults to F				  	[/!]
[!]			noLine 		- display <br> at end: T/F (Optional) 	[/!]
[!] 							Defaults to F				  	[/!]
[!]	Returns:  										 		  	[/!]
[!]		The constructed Variable Line				 			[/!]
[!]=============================================================[/!]
[Function name=printFV]
	
	[If ("[formatIndex]"="") | ("[formatIndex]"="[RAW][formatIndex][/RAW]")]
		[Then]
			[text]formatIndex=T[/text]
		[/Then]
	[/If]
	
	[If ("[oneLine]"="") | ("[oneLine]"="[RAW][oneLine][/RAW]")]
		[Then]
			[text]oneLine=F[/text]
		[/Then]
	[/If]
	
	[RETURN]Formvariables:<br>[/RETURN]
	
	[Formvariables]
		[Hideif [ignore]^|[name]|]
			[RETURN][i_build_line pFormatIndex=[formatIndex]&pIndex=[index]&pName=[name]&pValue=[value]][/RETURN]
			[Hideif [oneLine]=T]
				[RETURN]<br>[/RETURN]
			[/Hideif]
			[Showif [oneLine]=T]
				[RETURN], [/RETURN]
			[/Showif]
		[/Hideif]
	[/Formvariables]
	
	[Hideif [noLine]=T]
		[RETURN]<br>[/RETURN]
	[/Hideif]
[/Function]

[!]=============================================================[/!]
[!]	Prints the Listvariables to the page.						[/!]
[!] Parameters: 												[/!]
[!] 		formatIndex - display the Index: T/F (Optional)   	[/!]
[!] 							Defaults to T				  	[/!]
[!] 		oneLine 	- display on One Line: T/F (Optional) 	[/!]
[!] 							Defaults to F					[/!]
[!]			noLine 		- display <br> at end: T/F (Optional) 	[/!]
[!] 							Defaults to F				  	[/!]
[!]	Returns:  										 		  	[/!]
[!]		The constructed Variable Line				 			[/!]
[!]=============================================================[/!]
[Function name=printLV]

	[If ("[formatIndex]"="") | ("[formatIndex]"="[RAW][formatIndex][/RAW]")]
		[Then]
			[text]formatIndex=T[/text]
		[/Then]
	[/If]
	
	[If ("[oneLine]"="") | ("[oneLine]"="[RAW][oneLine][/RAW]")]
		[Then]
			[text]oneLine=F[/text]
		[/Then]
	[/If]
	
	[RETURN]Listvariables:<br>[/RETURN]
	
	[Listvariables]
		[Hideif [ignore]^|[name]|]
			[RETURN][i_build_line pFormatIndex=[formatIndex]&pIndex=[index]&pName=[name]&pValue=[value]][/RETURN]
			[Hideif [oneLine]=T]
				[RETURN]<br>[/RETURN]
			[/Hideif]
			[Showif [oneLine]=T]
				[RETURN], [/RETURN]
			[/Showif]
		[/Hideif]
	[/Listvariables]
	
	[Hideif [noLine]=T]
		[RETURN]<br>[/RETURN]
	[/Hideif]
[/Function]

[!]================================[/!]
[!]			Set JSON Header		   [/!]
[!]================================[/!]
[Function name=setJSONHeader]
	[RETURN][setMIMEHeader name=Content-Type&value=application/json][/RETURN]
[/Function]

[!]================================[/!]
[!]			Set XML Header		   [/!]
[!]================================[/!]
[Function name=setXMLHeader]
	[RETURN][setMIMEHeader name=Content-Type&value=application/xml][/RETURN]
[/Function]