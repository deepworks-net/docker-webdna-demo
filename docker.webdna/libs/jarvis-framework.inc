[!] DEFAULT USER LOGIN FUNCTION? [/!]

[!] DEFAULT FUNCTIONS [/!]
[!] THESE CAN BE OVERRIDDEN IN A SUBSEQUENT INC FILE - MAKE SURE THE SCOPE IS GLOBAL!!! [/!]

[!] Get Owner Settings (if not using 'owners' this can be anything really) [/!]
[Function name=JAPP_OWNER_SETTINGS][/Function]

[!] The basic login function. Returns T if login success, F otherwise [/!]
[Function name=JAPP_LOGIN_FUNC][RETURN]T[/RETURN][/Function]

[!] What to do if the login fails. [/!]
[Function name=JAPP_LOGIN_FAIL_FUNC][/Function]

[!] What to do if the login succeeds. [/!]
[Function name=JAPP_LOGIN_SUCCESS_FUNC][/Function]

[!] What to do if unauthorized. [/!]
[Function name=JAPP_UNAUTH_FUNC][/Function]

[!] What to do if the user logs out. [/!]
[Function name=JAPP_LOGOUT_FUNC][/Function]

[!] Extra checks to do before running the Login Function. Returns T if passes checks, F otherwise [/!]
[Function name=JAPP_PRE_LOGIN_FUNC][RETURN]T[/RETURN][/Function]

[!] The authentication function testing to see if a user's login is valid. Returns T if valid, F otherwise. [/!]
[Function name=JAPP_AUTHENTICATE_FUNC][RETURN]T[/RETURN][/Function]

[!] The function Re-Authenticates a user. Returns T if successful, F otherwise. [/!]
[Function name=JAPP_REAUTH_FUNC][RETURN]T[/RETURN][/Function]

[!] A function that can call additional pre authorizaion functions, I.E. Redeem Codes, Forget Password, ect... [/!]
[Function name=JAPP_PRE_AUTH_FUNC][/Function]

[!] A function that can call additional base authorizaion functions, I.E. Switching Owners, ect... [/!]
[Function name=JAPP_BASE_AUTH_FUNC][/Function]

[!] FRAMEWORK FUNCTIONS - THESE SHOULD NOT BE OVERRIDDEN [/!]
[!] GET APP PATH AUTOMAGICALLY[/!]
[Function name=J_GET_APP_PATH]
	[RETURN]/[Listpath path=[file]&pathonly=T][name]/[/Listpath][/RETURN]
[/Function]

[Function name=J_GET_JAPP_DIRVARS_UT]
	[RETURN][ListPath path=[path]&PathOnly=T]/[name][/ListPath]/../_japp/shared/app.inc[/RETURN]
[/Function]

[!] GET APP PATH AUTOMAGICALLY[/!]
[Function name=J_GET_JAPP_APP_PATH]
	[RETURN]/[Listpath path=[file]&pathonly=T][name]/[/Listpath]../_japp/shared/[/RETURN]
[/Function]

[!] JARVIS GET DIRVARS FUNCTION [/!]
[Function name=J_DIRVARS]
	[text]_path=[path][/text]
	[Showif [isNull varName=path]=T][text]_path=../shared[/text][/Showif]
	[Search db=[_path]/dirvars.db&eqSTATUSdatarq=1&SORTORDERsort=1&SORTORDERtype=num]
		[Founditems]
			[text scope=GLOBAL][VARNAME]=[INTERPRET][VARVALUE][/INTERPRET][/text]
		[/Founditems]
	[/Search]
[/Function]

[!] JARVIS GET DIRVARS FUNCTION [/!]
[Function scope=GLOBAL&name=J_DIRVARS2]
	[SQL DSN=[j_dbcatalog]&username=[j_dbuservar]&password=[j_dbpassvar]&statement=SELECT nameVar,valueVar FROM sys_DirVars WHERE status = '1' AND installKey = '[installKey]' ORDER BY sortorder asc;]
		[Founditems]
			[text scope=GLOBAL][nameVar]=[INTERPRET][valueVar][/INTERPRET][/text]
		[/Founditems]
	[/SQL]
[/Function]

[!] JARVIS GET CONNECTIONS PATH FUNCTION [/!]
[Function name=J_CONNECTIONPATH]
	[text]_path=[path][/text]
	[Showif [isNull varName=path]=T][text]_path=../shared[/text][/Showif]
	[RETURN][_path]/connections_encrypted.db[/RETURN]
[/Function]

[Function scope=GLOBAL&name=J_APPSETTINGSPATH]
	[text]_path=[path][/text]
	[Showif [isNull varName=path]=T][text]_path=../shared[/text][/Showif]
	[RETURN][_path]/app_settings.db[/RETURN]
[/Function]

[Function scope=GLOBAL&name=J_APP_SETTINGS]
	[Search db=[J_APPSETTINGSPATH path=[Hideif [isNull varName=file]=T][URL][J_GET_APP_PATH][/URL][/Hideif]]&neKEYdatarq=[blank]]
		[Founditems]
			[text scope=GLOBAL][key]=[INTERPRET][val][/INTERPRET][/text]
		[/Founditems]
	[/Search]
[/Function]

[!] JARVIS LOGIN FUNCTION [/!]
[Function name=J_AUTH_LOGIN]
	[text]res=T[/text]
	[Showif [login]=T]
		[!] ============================== [/!]
		[!] 	Check Empty Username	   [/!]
		[!] ============================== [/!]
		[Showif [isNull varName=usernamevar]=T]
			[text]res=F[/text]
		[/Showif]
		[!] ============================== [/!]
		[!] 	Check Empty Password	   [/!]
		[!] ============================== [/!]
		[Showif [isNull varName=passwordvar]=T]
			[text]res=F[/text]
		[/Showif]
		[!] ============================== [/!]
		[!]    RUN APP LOGIN FUNC SHTUFF   [/!]
		[!] ============================== [/!]
		[Showif [JAPP_PRE_LOGIN_FUNC]=F]
			[text]res=F[/text]
		[/Showif]
		
		[Hideif [res]=F]
			[!] APP'S LOGIN FUNCTION [/!]
			[text]res=[JAPP_LOGIN_FUNC][/text]
		[/Hideif]
	[/Showif]
	[RETURN][res][/RETURN]
[/Function]

[!] JARVIS LOGOUT FUNCTION (Needed?) [/!]
[Function name=J_AUTH_LOGOUT]
	[text]res=T[/text]
	[RETURN][res][/RETURN]
[/Function]

[!] BASE AUTHENTICATION FUNCTION [/!]
[Function name=J_BASE_AUTH]
	[text]res=T[/text]
	
	[!] ================================== [/!]
	[!]  Secret Key for App Signature (?)  [/!]
	[!] ================================== [/!]
	[!][getHDKey][/!]
	
	[!] Handle Login Function [/!]
	[Showif [login]=T]
		[IF "[text show=t]res=[J_AUTH_LOGIN][/text]"="T"]
			[THEN][JAPP_LOGIN_SUCCESS_FUNC][/THEN]
			[ELSE][JAPP_LOGIN_FAIL_FUNC][/ELSE]
		[/IF]
	[/Showif]
	
	[Showif [logout]=T]
		[JAPP_LOGOUT_FUNC]
	[/Showif]
	
	[!] Is this where we place a OR function to say switch districts, ect? [/!]
	[JAPP_PRE_AUTH_FUNC]
	
	[RETURN][res][/RETURN]
[/Function]

[!] AUTHENTICATION FUNCTION FOR THE JARVIS FRAMEWORK [/!]
[Function name=J_AUTHENTICATE]
	[text]res=[J_BASE_AUTH][/text]

	[!] ================================== [/!]
	[!] Auth Token and Redirect If Failure [/!]
	[!] ================================== [/!]
	
	[Hideif [noAuth]=T]
		[Hideif [res]=F]
			[text]res=[JAPP_AUTHENTICATE_FUNC][/text]
		[/Hideif]
		[!] REAUTH FUNC SHOULD GO HERE! [/!]
	[/Hideif]
	
	[!] Is this where we place a OR function to say switch districts, ect? [/!]
	[JAPP_BASE_AUTH_FUNC]
	
	[RETURN][res][/RETURN]
[/Function]

[!] HEADER FUNCTION FOR THE JARVIS FRAMEWORK [/!]
[Function name=J_HEADER]
	[text]res=T[/text]
	[!] ============================================= [/!]
	[!] 	    									  [/!]
	[!] 	Disable Default Authorization Include     [/!]
	[!] 	Pass noAuth=T To declare alt auth.inc	  [/!]
	[!] 	    									  [/!]
	[!] ============================================= [/!]
	[text]res=[J_AUTHENTICATE][/text]
	
	[Showif [res]=F]
		[JAPP_UNAUTH_FUNC]
	[/Showif]
	[RETURN][Showif [show]=t][res][/Showif][/RETURN]
[/Function]



[!] =========================== [/!]
[!]                             [/!]
[!]     TOP LEVEL FUNCTIONS     [/!]
[!]                             [/!]
[!] =========================== [/!]
[!] JARVIS TOP LEVEL APP CORE CALL [/!]
[Function name=J_LOAD_APP]	
	[text]_appPath=includes/app.inc[/text]
	[Hideif [isNull varName=appPath]=T]
		[text]_appPath=[appPath][/text]
	[/Hideif]
	[SCOPE name=AppVars]
		[include file=[_appPath]]
		[ListVariables scope=AppVars]
			[Hideif [name]=appPath]
				[RETURN][text scope=global][name]=[value][/text][/RETURN]
			[/Hideif]
		[/ListVariables]
	[/SCOPE]
[/Function]

[Function name=J_SETUP_Settings]
	[SCOPE name=Settings_Vars]
		[text]_settingsPath=includes/settings.inc[/text]
		[Hideif [isNull varName=settingsPath]=T]
			[text]_settingsPath=[settingsPath][/text]
		[/Hideif]
		[!] HIDE IF THIS DOES NOT EXIST!?!?!?! [/!]
		[include file=[_settingsPath]]
		[ListVariables scope=Settings_Vars]
			[Hideif [name]^settingsPath]
				[RETURN][text scope=global][name]=[value][/text][/RETURN]
			[/Hideif]
		[/ListVariables]
	[/SCOPE]
[/Function]

[!] MAIN TOP DIR FUNCTION TO CALL [/!]
[Function name=J_SETUP_App]	
	[Showif [isNull varName=page]=T][text]page=index.html[/text][/Showif]
	[RETURN][GetChars start=1&trim=both][J_LOAD_APP appPath=[appPath]][Hideif [noSettings]=T][J_SETUP_Settings settingsPath=[settingsPath]][/Hideif][Showif [isNull varName=replaceDir]=T][text]replaceDir=[clientDir][/text][/Showif][include file=[GREP search=/[ListPath path=[safe_thisurl]&PathOnly=T][name]/[/ListPath]&replace=[INTERPRET][replaceDir][/INTERPRET]/][safe_thisurl][Showif [ListPath path=[safe_thisurl]&fileOnly=T][name][/ListPath]=][page][/Showif][/GREP]][/GetChars][/RETURN]
[/Function]

[!] MAIN TOP DIR FUNCTION TO CALL FOR SERVER CONNECTIVITY [/!]
[Function name=J_SETUP_ServerPortal]
	[RETURN][GetChars start=1&trim=both][J_LOAD_APP appPath=[appPath]][Hideif [noSettings]=T][J_SETUP_Settings settingsPath=[settingsPath]][/Hideif][Showif [isNull varName=replaceDir]=T][text]replaceDir=[serverDir][/text][/Showif][include file=[GREP search=/[ListPath path=[safe_thisurl]&PathOnly=T][name]/[/ListPath]&replace=[INTERPRET][replaceDir][/INTERPRET]/][safe_thisurl][/GREP]][/GetChars][/RETURN]
[/Function]

[!] MAIN TOP DIR FUNCTION TO CALL [/!]
[Function name=J_SETUP_API_App]
	[text]_dir=[apiDir][/text]
	[Hideif [isNull varName=replaceDir]=T][text]_dir=[replaceDir][/text][/Hideif]
	[RETURN][J_SETUP_App replaceDir=[_dir]][/RETURN]
[/Function]

[!] MAIN TOP DIR FUNCTION TO CALL [/!]
[Function name=J_SETUP_JSX]
	[RETURN][setMIMEHeader name=Content-Type&value=application/x-javascript][/RETURN]
	[text]_dir=[scriptDir][/text]
	[Hideif [isNull varName=replaceDir]=T][text]_dir=[replaceDir][/text][/Hideif]
	[RETURN][GetChars start=1&trim=both][J_LOAD_APP appPath=[appPath]][Hideif [noSettings]=T][J_SETUP_Settings settingsPath=[settingsPath]][/Hideif][Showif [isNull varName=replaceDir]=T][text]replaceDir=[scriptDir][/text][/Showif][include file=[GREP search=/[ListPath path=[safe_thisurl]&PathOnly=T][name]/[/ListPath]&replace=[INTERPRET][replaceDir][/INTERPRET]/][safe_thisurl][/GREP]][/GetChars][/RETURN]
[/Function]


[!] Get The Current Script Dir - TEMP??? [/!]
[Function name=J_GetScriptURL]
	[text]res=[a_dir][/text]
	[Showif [isNull varName=a_dir]=T]
		[text]res=[assetsDir][/text]
	[/Showif]
	
	[text]_topDir=[t_Dir][/text]
	[Showif [isNull varName=t_dir]=T]
		[text]_topDir=[ownerDir][/text]
	[/Showif]
	
	[text]curPath=[ListPath path=[safe_thisurl]&pathONLY=T]/[name][/ListPath][/text]
	[text]thepath=[GREP search=/safe[_topDir]&replace=][curPath][/GREP][/text]
	
	[text]res=[res]/scripts[thePath][/text]
	
	[Showif [debug]=1]
		[text]res=[ownerDir][thePath]/scripts[/text]
	[/Showif]
	[RETURN][res]/[scriptName][Hideif [debug]=1].min[/Hideif].js[Showif [debug]=1]x[/Showif]?v=[app_ver][/RETURN]
[/Function]