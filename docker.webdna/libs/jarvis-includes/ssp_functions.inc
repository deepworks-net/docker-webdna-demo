[!] Make in Globals? [/!]
[Function name=J_makeQ]
	[RETURN]SELECT TOP(500) * FROM ( [query] ) AS RowResult WHERE RowNum <= [MATH][start]+[length][/MATH] AND RowNum > [start] ORDER BY RowNum;[/RETURN]
[/Function]

[Function name=J_getVar]
	[RETURN][Hideif [isNull varName=name]=T][FormVariables name=[name]][value][/FormVariables][/Hideif][/RETURN]
[/Function]

[Function name=J_getRowString]
	[RETURN]ROW_NUMBER() OVER ([Hideif [isNull varName=orderVar]=T]ORDER BY [orderVar][/Hideif]) AS RowNum[/RETURN]
[/Function]

[Function name=J_getColumnCount]
	[text]colCount=0[/text]
	[FormVariables name=columns&exact=F]
		[Showif [name]^[RAW][name][/RAW]]
			[text]colCount=[MATH][colCount]+1[/MATH][/text]
		[/Showif]
	[/FormVariables]
	[RETURN][colCount][/RETURN]
[/Function]

[Function name=J_getOrderCount]
	[text]orderCount=0[/text]
	[FormVariables name=order&exact=F]
		[Showif [name]^[RAW][column][/RAW]]
			[text]orderCount=[MATH][orderCount]+1[/MATH][/text]
		[/Showif]
	[/FormVariables]
	[RETURN][orderCount][/RETURN]
[/Function]

[Function name=J_getVisibleList]
	[text]lst=|[/text]
	[Hideif [isNull varName=colCount]=T]
		[Loop start=0&end=[MATH][colCount]-1[/MATH]]
			[text]nameit=[J_getVar name=columns[[index]][RAW][name][/RAW]][/text]
			[text]visible=[J_getVar name=columns[[index]][RAW][visible][/RAW]][/text]
			[Showif [validCols]^|[nameit]|]
				[Showif [visible]=true]
					[text]lst=[lst][nameit]|[/text]
				[/Showif]
			[/Showif]
		[/Loop]
	[/Hideif]
	[RETURN][lst][/RETURN]
[/Function]

[Function name=J_getSearchVar]
	[text]firstone=f[/text]
	[text]sVal=[/text]
	[Hideif [isNull varName=colCount]=T]
		[Loop start=0&end=[MATH][colCount]-1[/MATH]]
			[text]nameit=[J_getVar name=columns[[index]][RAW][name][/RAW]][/text]
			[text]searchable=[J_getVar name=columns[[index]][RAW][searchable][/RAW]][/text]
			[Showif [validCols]^|[nameit]|]
				[Hideif [isNull varName=searchVal]=T]
					[Showif [searchable]=true]
						[text]sVal=[sVal][Hideif [firstone]=f] OR[/Hideif] [nameit] LIKE [URL]'%[CleanVar varName=searchVal]%'[/URL][/text]
						[text]firstone=t[/text]
					[/Showif]
				[/Hideif]
			[/Showif]
		[/Loop]
	[/Hideif]
	[RETURN][sVal][/RETURN]
[/Function]

[Function name=J_getOrderVar]
	[text]firstone=f[/text]
	[text]orderVar=[/text]
	[Hideif [isNull varName=orderCount]=T]
		[Loop start=0&end=[MATH][orderCount]-1[/MATH]]
			[text]orderCol=[J_getVar name=order[[index]][RAW][column][/RAW]][/text]
			[text]orderDir=[J_getVar name=order[[index]][RAW][dir][/RAW]][/text]
			[text]orderColName=[J_getVar name=columns[[orderCol]][RAW][name][/RAW]][/text]
			[text]orderable=[J_getVar name=columns[[orderCol]][RAW][orderable][/RAW]][/text]
			[Showif [validCols]^|[orderColName]|]
				[Showif [orderable]=true]
					[text]orderVar=[orderVar][Hideif [firstone]=f],[/Hideif][orderColName] [orderDir][/text]
					[text]firstone=t[/text]
				[/Showif]
			[/Showif]
		[/Loop]
	[/Hideif]
	[RETURN][orderVar][/RETURN]
[/Function]