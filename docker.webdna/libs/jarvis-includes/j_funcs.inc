[!] Declare Sections [/!]
[Function name=declareSections]
	[text scope=GLOBAL]_possibleSections=[sections][/text]
	[!] Find Out What We Are Asking For? (sections or all) [/!]
	[!] Build Sections List [/!]
	[text scope=GLOBAL]_sections=|[/text]
	[FormVariables scope=GLOBAL&name=[name]]
		[Showif [_possibleSections]^|[value]|]
			[text scope=GLOBAL]_sections=[_sections][value]|[/text]
		[/Showif]
	[/FormVariables]
	[ListVariables scope=GLOBAL&name=[name]]
		[Showif [_possibleSections]^|[value]|]
			[text scope=GLOBAL]_sections=[_sections][value]|[/text]
		[/Showif]
	[/ListVariables]
	[Showif [_sections]=|]
		[text scope=GLOBAL]_sections=|all|[/text]
	[/Showif]
[/Function]

[!] Quick Function to check for the specific section [/!]
[Function name=showSection]
	[text]res=F[/text]
	[IF ("[_sections]"^"|[thisSec]|")[Hideif [noAll]=T] | ("[_sections]"^"|all|")[/Hideif]]
		[THEN][text]res=T[/text][/THEN]
	[/IF]
	[RETURN][res][/RETURN]
[/Function]

[!] Build A Piped List [/!]
[Function name=getList]
	[FormVariables name=[listNameVar]&exact=F]
		[RETURN][text scope=GLOBAL][listVar]=[INTERPRET][[listVar]][/INTERPRET][value]|[/text][/RETURN]
	[/FormVariables]
[/Function]

[Function name=makeLists]
	[Hideif [isNull varName=_possibleSections]=T]
		[text]_pS=[sections][/text]
		[Showif [isNull varName=sections]=T]
			[text]_pS=[_possibleSections][/text]
		[/Showif]
		[ListWords words=[_pS]&delimiters=|]
			[RETURN][text scope=GLOBAL][word]List=|[/text][/RETURN]
		[/ListWords]
	[/Hideif]
[/Function]

[Function name=setLists]
	[Hideif [isNull varName=_possibleSections]=T]
		[text]_pS=[sections][/text]
		[Showif [isNull varName=sections]=T]
			[text]_pS=[_possibleSections][/text]
		[/Showif]
		[ListWords words=[_pS]&delimiters=|]
			[FormVariables name=[word]ID]
				[RETURN][text scope=GLOBAL][word]List=[INTERPRET][[word]List][/INTERPRET][value]|[/text][/RETURN]
			[/FormVariables]
			[ListVariables name=[word]ID]
				[RETURN][text scope=GLOBAL][word]List=[INTERPRET][[word]List][/INTERPRET][value]|[/text][/RETURN]
			[/ListVariables]
		[/ListWords]
	[/Hideif]
[/Function]

[!] Check to see if we should place a comma at the end of an object. [/!]
[Function name=showComma]
	[text]res=F[/text]
	[IF "[_sections]"^"|all|"]
		[THEN][text]res=T[/text][/THEN]
		[ELSE]
			[text]hide=1[/text]
			[Listwords words=[_possibleSections]&delimiters=|]
				[Hideif [hide]=1]
					[Showif [_sections]^|[word]|]
						[text]res=T[/text]
					[/Showif]
				[/Hideif]
				[Showif [word]=[thisSec]]
					[text]hide=0[/text]
				[/Showif]
			[/Listwords]
		[/ELSE]
	[/IF]
	[RETURN][res][/RETURN]
[/Function]

[Function name=errorLog]
	[redirect [serverDir]/error.tpl?errmsg=[_perrmsg]]
[/Function]

[Function scope=GLOBAL&name=changeLog][/Function]

[Function scope=GLOBAL&name=J_InsertCallback][/Function]
[Function scope=GLOBAL&name=J_UpdateCallback][/Function]

[Function name=CheckSection]
	
	[Showif [isNull varName=manualList]=T]
		[text]manualList=[/text]
	[/Showif]
	
	[!] CDOS NEEDS TO BE CAREFUL OF THIS!!! [/!]
	[Showif [isNull varName=ownerField]=T]
		[text]ownerField=r_ownerid[/text]
	[/Showif]
	
	[!] Insert Stuff [/!]
	[text]_insertFields=[J_BuildInFields listMap=[valueList]][/text]
	
	[!] Update Stuff [/!]		
	[text]_updateFields=[J_BuildUpFields listMap=[valueList]][/text]
	[text]valueMap=[J_BuildValueMap listMap=[valueList]][/text]
	
	[!] GET NEW RECORDS [/!]
	[text scope=GLOBAL]_newList=|[/text]
	[getList listNameVar=[addVarTemp]|sku|&listVar=_newList]
	[text scope=GLOBAL]newList=[_newList][/text]
	
	[!] GET UPDATED RECORDS [/!]
	[getList listNameVar=[editVarTemp]|sku|&listVar=[editListVar]]
	
	[!] GET DELETED RECORDS [/!]
	[text scope=GLOBAL]_deleteList=|[/text]
	[getList listNameVar=delete|sku|&listVar=_deleteList]
	[text]_DCount=[CountWords delimiters=|][_deleteList][/CountWords][/text]
	
	[text]_InsertString=[/text]
	
	[!] Check for 'Empty' Adds and ignore them [/!]
	[text]fNewList=|[/text]
	[ListWords words=[_newList]&delimiters=|]
		[Hideif [J_EmptyCheck listMap=[valueList]&ignore=[ignore]&s=[word]&temp=[addVarTemp]]=T]
			[text]fNewList=[fNewList][word]|[/text]
		[/Hideif]
	[/ListWords]
	[text]_ACount=[CountWords delimiters=|][fNewList][/CountWords][/text]
	[ListWords words=[fNewList]&delimiters=|]
		[text]_InsertString=[_InsertString][J_BuildInString temp=[addVarTemp]&listMap=[valueList]&s=[word]][Hideif [index]=[_ACount]],[/Hideif][/text]
	[/ListWords]
	
	[text]changeList=|[/text]
	[Showif [checkChanges]=T]
		[Hideif [INTERPRET][[editListVar]][/INTERPRET]=|]
			[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=SELECT sku[Listwords words=[valueList][manualList]&delimiters=|],[word][/Listwords] FROM [tableVar] WHERE '[INTERPRET][[editListVar]][/INTERPRET]' LIKE [URL]'%|'+cast(Sku AS varchar(10))+'|%'[/URL][Hideif [noOwner]=T] AND [ownerField] = '[ownervar]'[/Hideif];]
				[Founditems]
					[Listwords words=[valueList][manualList]&delimiters=|]
						[text]newVal=[INTERPRET][[editVarTemp]|[word]|[sku]][/INTERPRET][/text]
						[Showif [isNull varName=[editVarTemp]|[word]|[sku]]=T]
							[text]newVal=[/text]
						[/Showif]
						[text]origVal=[INTERPRET][[word]][/INTERPRET][/text]
						[Showif [dateList]^|[word]|]
							[text]origVal=[CleanDate theDate=[origVal]][/text]
						[/Showif]
						[Showif [timeList]^|[word]|]
							[text]origVal=[CleanJustTime theTime=[origVal]][/text]
						[/Showif]
						[Hideif [URL][origVal][/URL]=[GREP search=[URL]%0D%0A[/URL]&replace=[URL]%0D[/URL]][URL][newVal][/URL][/GREP]]
							[changeLog logAction=edit]
							[Hideif [changeList]^|[sku]|]
								[text]changeList=[changeList][sku]|[/text]
							[/Hideif]
						[/Hideif]
					[/ListWords]
				[/Founditems]
			[/SQL]
			[text][editListVar]=[changeList][/text]
		[/Hideif]
	[/Showif]
	
	[text scope=GLOBAL]orig[editListVar]=[INTERPRET][[editListVar]][/INTERPRET][/text]
	
	[text]_UpdateString=[/text]
	[text]_ECount=[CountWords delimiters=|][INTERPRET][[editListVar]][/INTERPRET][/CountWords][/text]
	[ListWords words=[INTERPRET][[editListVar]][/INTERPRET]&delimiters=|]
		[text]_UpdateString=[_UpdateString][J_BuildUpString temp=[editVarTemp]&listMap=[valueList]&s=[word]][Hideif [index]=[_ECount]],[/Hideif][/text]
	[/ListWords]
	
	[Showif [_ACount]!0]
		[text]doAdds=1[/text]
	[/Showif]
	
	[Showif [_ECount]!0]
		[text]doUpdates=1[/text]
	[/Showif]
	
	[Showif [_DCount]!0]
		[text]doDeletes=1[/text]
	[/Showif]
	
	[!] Make a Passed Variable!? [/!]
	[text scope=GLOBAL]addedList=|[/text]
	[text scope=GLOBAL]addedMappings=|[/text]
	[!] ADD [/!]
	[Showif [doAdds]=1]
		[!] Wrap for Errors !!! [/!]
		[text]err=T[/text]
	
		[Showif [GetChars start=1&trim=both]
			[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=[URL][J_InsertTemplate tableVar=[tableVar]&insertFields=[_InsertFields]&insertVar=[_InsertString]][/URL]]
				[Founditems]
					[text scope=GLOBAL][editListVar]=[INTERPRET][[editListVar]][/INTERPRET][sku]|[/text]
					[text scope=GLOBAL]addedList=[addedList][sku]|[/text]
					[changeLog logAction=add]
					[Showif [J_InsertCallback]=T]ERROR[/Showif]
				[/Founditems]
			[/SQL][/GetChars]=]
				[text]err=F[/text]
		[/Showif]
	
		[text scope=GLOBAL][editListVar]=[INTERPRET][[editListVar]][/INTERPRET][/text]
		
		[Showif [err]=T]
			[errorLog]
		[/Showif]
		
		[J_SuccessMsg]
	
	[/Showif]
	
	[!] UPDATE [/!]
	[Showif [doUpdates]=1]
		[!] Wrap for Errors !!! [/!]
		[text]err=T[/text]
		[Showif [GetChars start=1&trim=both]
			[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=[URL][J_UpdateTemplate tableVar=[tableVar]&upFields=[_updateFields]&updateVar=[_UpdateString]][/URL]]
				[Founditems]
					[Showif [J_UpdateCallback]=T]ERROR[/Showif]
				[/Founditems]
			[/SQL][/GetChars]=]
				[text]err=F[/text]
		[/Showif]
		
		[Showif [err]=T]
			[errorLog]
		[/Showif]

		[J_SuccessMsg]
		
	[/Showif]
	
	[!] DELETES [/!]
	[Showif [doDeletes]=1]
		[ListWords words=[_deleteList]&delimiters=|]
			
			[!] Wrap for Errors !!! [/!]
			[text]err=T[/text]
			[Showif [GetChars start=1&trim=both]
				[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=DELETE FROM [tableVar] OUTPUT DELETED.SKU WHERE sku = '[word]';]
					[Founditems]
						[DELETE db=[dbDir]/[tableVar].db&eqSKUdatarq=[sku]]
					[/Founditems]
				[/SQL][/GetChars]=]
					[text]err=F[/text]
			[/Showif]
			
			[Showif [err]=T]
				[errorLog]
			[/Showif]
			
			[J_SuccessMsg]
			
		[/ListWords]
	[/Showif]
	
[/Function]

[Function name=CheckSortOrder]

	[text]nS=[sortOrder][/text]
	
[TABLE scope=GLOBAL&name=DNA_SORTTABLE&fields=sku,nS,cS,action]
[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=SELECT sku,sortOrder FROM [tableVar] WHERE sku > '0' [Hideif [isNull varName=tableParms]=T][tableParms][/Hideif];][FOUNDITEMS][sku]	[Hideif [sku]=[sortSku]][sortOrder][/Hideif][Showif [sku]=[sortSku]][nS][/Showif]	[sortOrder]	[Hideif [sku]=[sortSku]]0[/Hideif][Showif [sku]=[sortSku]]1[/Showif]
[/Founditems][/SQL][/TABLE]
	
	[text]cS=[lookup table=DNA_SORTTABLE&value=[sortSku]&lookinfield=SKU&returnfield=CS][/text]
	
	[text]r=[MATH][nS]-[cS][/MATH][/text]
	
	[Hideif [r]=0]
	
		[IF ("[r]">"0")]
			[THEN]
				[text]inc=-1[/text]
				[!] Positive Difference, we are changing the order down [/!]
				[SEARCH table=DNA_SORTTABLE&GROUP1Fields=CS&GROUP2Fields=CS&grGROUP1datarq=[cS]&leGROUP2datarq=[nS]&GROUP1type=num&GROUP2type=num]
					[Founditems]
						[text]nSO=[MATH][cS]+[inc][/MATH][/text]
						[REPLACE table=DNA_SORTTABLE&eqSKUdatarq=[sku]][!]
							[/!]nS=[nSO]&action=1[!]
						[/!][/REPLACE]
					[/Founditems]
				[/Search]
				
			[/THEN]
			[ELSE]
				[text]inc=1[/text]
				[!] Negative Difference, we are changing the order up [/!]
				[SEARCH table=DNA_SORTTABLE&GROUP1Fields=CS&GROUP2Fields=CS&lsGROUP1datarq=[cS]&geGROUP2datarq=[nS]&GROUP1type=num&GROUP2type=num]
					[Founditems]
						[text]nSO=[MATH][cS]+[inc][/MATH][/text]
						[REPLACE table=DNA_SORTTABLE&eqSKUdatarq=[sku]][!]
							[/!]nS=[nSO]&action=1[!]
						[/!][/REPLACE]
					[/Founditems]
				[/Search]
				
			[/ELSE]
		[/IF]
		
		[text]valueMap=[J_BuildValueMap listMap=[valueList]][/text]
		
		[text]_UpdateString=[/text]
		[SEARCH table=DNA_SORTTABLE&eqACTIONdatarq=1]
			[Founditems]
				[text scope=GLOBAL][editListVar]=[INTERPRET][[editListVar]][/INTERPRET][sku]|[/text]
				[text]_UpdateString=[_UpdateString][J_BuildUpString listMap=|nS|][Hideif [index]=[numfound]],[/Hideif][/text]
			[/Founditems]
		[/Search]
		
		[text]_updateFields=[J_BuildUpFields listMap=[valueList]][/text]
		
		[!] UPDATE [/!]
		[!] Wrap for Errors !!! [/!]
		[text]err=T[/text]
		[Showif [GetChars start=1&trim=both]
			[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=[URL][J_UpdateTemplate tableVar=[tableVar]&outputClause=[URL][outputClause][/URL]&upFields=[_updateFields]&updateVar=[_UpdateString]][/URL]]
			[/SQL][/GetChars]=]
				[text]err=F[/text]
		[/Showif]
		
		[Showif [err]=T]
			[redirect [serverDir]/error.tpl?errmsg=[_perrmsg]]
		[/Showif]
		
		[J_SuccessMsg]
		
	[/Hideif]
		
[/Function]

[Function name=CheckSortOrder_DirVars]
	[Showif [isNull varName=dirvarsDB]=T][text]dirvarsDB=dirvars.db[/text][/Showif]
	[text]nS=[sortOrder][/text]

[TABLE scope=GLOBAL&name=DNA_SORTTABLE&fields=sku,nS,cS,action]
[Search db=[j_sharedDir]/[dirvarsDB]&neSORTORDERdatarq=[blank]][FOUNDITEMS][sku]	[Hideif [sku]=[sortSku]][sortOrder][/Hideif][Showif [sku]=[sortSku]][nS][/Showif]	[sortOrder]	[Hideif [sku]=[sortSku]]0[/Hideif][Showif [sku]=[sortSku]]1[/Showif]
[/Founditems][/Search][/TABLE]
	
	[text]cS=[lookup table=DNA_SORTTABLE&value=[sortSku]&lookinfield=SKU&returnfield=CS][/text]
	
	[text]r=[MATH][nS]-[cS][/MATH][/text]
	
	[Hideif [r]=0]
	
		[IF ("[r]">"0")]
			[THEN]
				[text]inc=-1[/text]
				[!] Positive Difference, we are changing the order down [/!]
				[SEARCH table=DNA_SORTTABLE&GROUP1Fields=CS&GROUP2Fields=CS&grGROUP1datarq=[cS]&leGROUP2datarq=[nS]&GROUP1type=num&GROUP2type=num]
					[Founditems]
						[text]nSO=[MATH][cS]+[inc][/MATH][/text]
						[REPLACE table=DNA_SORTTABLE&eqSKUdatarq=[sku]][!]
							[/!]nS=[nSO]&action=1[!]
						[/!][/REPLACE]
					[/Founditems]
				[/Search]
				
			[/THEN]
			[ELSE]
				[text]inc=1[/text]
				[!] Negative Difference, we are changing the order up [/!]
				[SEARCH table=DNA_SORTTABLE&GROUP1Fields=CS&GROUP2Fields=CS&lsGROUP1datarq=[cS]&geGROUP2datarq=[nS]&GROUP1type=num&GROUP2type=num]
					[Founditems]
						[text]nSO=[MATH][cS]+[inc][/MATH][/text]
						[REPLACE table=DNA_SORTTABLE&eqSKUdatarq=[sku]][!]
							[/!]nS=[nSO]&action=1[!]
						[/!][/REPLACE]
					[/Founditems]
				[/Search]
				
			[/ELSE]
		[/IF]
		
		[SEARCH table=DNA_SORTTABLE&eqACTIONdatarq=1]
			[Founditems]
				[!] UPDATE [/!]
				[!] Wrap for Errors !!! [/!]
				[text]err=T[/text]
				[Showif [GetChars start=1&trim=both]
					[REPLACE db=[j_sharedDir]/[dirvarsDB]&eqSKUdatarq=[sku]][!]
						[/!]sortOrder=[ns][!]
					[/!][/REPLACE]
					[REPLACE db=[sharedDir]/[dirvarsDB]&eqSKUdatarq=[sku]][!]
						[/!]sortOrder=[ns][!]
					[/!][/REPLACE]
					[/GetChars]=]
						[text]err=F[/text]
				[/Showif]
				
				[Showif [err]=T]
					[redirect [serverDir]/error.tpl?errmsg=[_perrmsg]]
				[/Showif]
				
			[/Founditems]
		[/SEARCH]
		
		[J_SuccessMsg]
		
	[/Hideif]
[/Function]