[!] CREATE MAP TABLE and Valid Cols List! [/!]
[Function name=J_MakeMapTable]
	[IF ("[isNull varName=tableVar]")="T" | ("[isNull varName=schemaVar]")]
		[THEN]
			[RETURN]ERROR - NO TABLE OR SCHEMA PASSED![/RETURN]
		[/THEN]
		[ELSE]
			[Showif [isNull varName=tableDBVar]=T][text]tableDBVar=[/text][/Showif]
			[text]_vCols=|[/text]
[RETURN]
[!] Make A Table That Has All Mappings! [/!]
[TABLE scope=GLOBAL&name=DNA_J_Mappings&fields=SKU,ALIAS,SEARCHFIELD,ORDERFIELD]
[SQL DSN=[dbcatalog]&username=[dbuservar]&password=[dbpassvar]&statement=SELECT sku,alias,searchField,orderField FROM vi_sys_SSPMaps WHERE status = '1' AND schemaVar = '[CleanVar varName=schemaVar]' AND viewVar = '[CleanVar varName=tableVar]' ORDER BY sku;][Founditems][text]_vCols=[_vCols][alias]|[/text][sku]	[alias]	[tableDBVar][searchField]	[tableDBVar][orderField]
[/Founditems][/SQL][/TABLE]
[IF ("[isNull varName=validColsVarName]"="T"]
	[THEN][text scope=GLOBAL]validCols=[_vCols][/text][/THEN]
	[ELSE][text scope=GLOBAL][validColsVarName]=[_vCols][/text][/ELSE]
[/IF]
[/RETURN]
		[/ELSE]
	[/IF]
[/Function]

[!]  FILTER FUNCTIONS!  [/!]

[Function name=J_Get_Int_Val]
	[RETURN][MATH][GETCHARS start=1&trim=both][CleanVar varName=[val]][/GETCHARS]+0[/MATH][/RETURN]
[/Function]

[Function name=J_Get_String_Val]
	[RETURN][GETCHARS start=1&trim=both][CleanVar varName=[val]][/GETCHARS][/RETURN]
[/Function]

[Function name=J_Int_Filter]
	[RETURN][Hideif [isNull varName=[val]]=T] AND [field] = '[J_Get_Int_Val val=[val]]'[/Hideif][/RETURN]
[/Function]

[Function name=J_IntLike_Filter]
	[RETURN][Hideif [isNull varName=[val]]=T] AND [field] LIKE '[URL]%[J_Get_Int_Val val=[val]]%[/URL]'[/Hideif][/RETURN]
[/Function]

[Function name=J_String_Filter]
	[RETURN][Hideif [isNull varName=[val]]=T] AND [field] LIKE '[URL]%[J_Get_String_Val val=[val]]%[/URL]'[/Hideif][/RETURN]
[/Function]

[Function name=J_Multiple_Filter]
	[Showif [isNull varName=type]=T][text]type=String[/text][/Showif]
	[text]multiVar=[/text]
	[FormVariables name=[val]&exact=False]
		[text]multiVar=[multiVar][Hideif [index]=1],[/Hideif] '[INTERPRET][J_Get_[type]_Val val=value][/INTERPRET]'[/text]
	[/FormVariables]
	[Hideif [multiVar]=]
		[text]multiVar= AND ([field] IN ([multiVar]))[/text]
	[/Hideif]
	[RETURN][multiVar][/RETURN]
[/Function]

[Function name=J_Int_Multiple_Filter]
	[RETURN][J_Multiple_Filter val=[val]&field=[field]&type=Int][/RETURN]
[/Function]

[Function name=J_String_Multiple_Filter]
	[RETURN][J_Multiple_Filter val=[val]&field=[field]&type=String][/RETURN]
[/Function]

[Function name=J_Date_Filter]
	[text]tempSel=On[/text]
	[Hideif [isNull varName=[sel_val]]=T]
		[text]tempSel=[CleanVar varName=[sel_val]][/text]
	[/Hideif]
	[RETURN][Hideif [isNull varName=[s_val]]=T] AND CAST([field] AS DATE) [Showif [tempSel]=Before]<[/Showif][Showif [tempSel]=After]>[/Showif][Showif [tempSel]=On]=[/Showif][Showif [tempSel]=Between]BETWEEN[/Showif] '[J_Get_String_Val val=[s_val]]'[Showif [tempSel]=Between] AND '[J_Get_String_Val val=[e_val]]'[/Showif][/Hideif][/RETURN]
[/Function]

[Function name=J_Get_Filter_String]
	[Showif [isNull varName=filterVarName]=T]
		[text]filterVarName=filterVar[/text]
	[/Showif]
	[RETURN][text scope=GLOBAL][filterVarName]=[INTERPRET][[filterVarName]][J_[type]_Filter field=[field]&val=[val]][/INTERPRET][/text][/RETURN]
[/Function]


[!]  END FILTER FUNCTIONS  [/!]

[!] Make in Globals? [/!]
[Function name=J_makeQ]
	[Showif [isNull varName=topLimit]=T][text]topLimit=[length][/text][/Showif]
	[RETURN]SELECT TOP([topLimit]) * FROM ( [query] ) AS RowResult WHERE [RAW][index][/RAW] <= [MATH][start]+[length][/MATH] AND [RAW][index][/RAW] > [start] ORDER BY [RAW][index][/RAW][/RETURN]
[/Function]

[Function name=J_getVar]
	[RETURN][Hideif [isNull varName=name]=T][FormVariables name=[name]][value][/FormVariables][/Hideif][/RETURN]
[/Function]

[Function name=J_getRowString]
	[RETURN]ROW_NUMBER() OVER ([Hideif [isNull varName=orderVar]=T]ORDER BY [orderVar][/Hideif]) AS [RAW][index][/RAW][/RETURN]
[/Function]

[Function name=J_getJSONString]
	[RETURN][Hideif [isNull varName=query]=T]SELECT (ISNULL(CAST(([query] FOR JSON PATH, INCLUDE_NULL_VALUES) AS text), '[]')) as jsonvar[/Hideif][/RETURN]
[/Function]

[Function name=J_getColumnCount]
	[text]colCount=0[/text]
	[FormVariables name=columns&exact=F]
		[Showif [name]^[RAW][name][/RAW]]
			[text]colCount=[MATH][colCount]+1[/MATH][/text]
		[/Showif]
	[/FormVariables]
	[RETURN][colCount][/RETURN]
[/Function]

[Function name=J_getOrderCount]
	[text]orderCount=0[/text]
	[FormVariables name=order&exact=F]
		[Showif [name]^[RAW][column][/RAW]]
			[text]orderCount=[MATH][orderCount]+1[/MATH][/text]
		[/Showif]
	[/FormVariables]
	[RETURN][orderCount][/RETURN]
[/Function]

[Function name=J_getVisibleList]
	[text]lst=|[/text]
	[Hideif [isNull varName=colCount]=T]
		[Loop start=0&end=[MATH][colCount]-1[/MATH]]
			[text]nameit=[J_getVar name=columns[[index]][RAW][name][/RAW]][/text]
			[text]visible=[J_getVar name=columns[[index]][RAW][visible][/RAW]][/text]
			[Showif [validCols]^|[nameit]|]
				[Showif [visible]=true]
					[text]lst=[lst][nameit]|[/text]
				[/Showif]
			[/Showif]
		[/Loop]
	[/Hideif]
	[RETURN][lst][/RETURN]
[/Function]

[Function name=J_getSearchVar]
	[text]firstone=f[/text]
	[text]sVal=[/text]
	[Hideif [isNull varName=colCount]=T]
		[Loop start=0&end=[MATH][colCount]-1[/MATH]]
			[text]nameit=[J_getVar name=columns[[index]][RAW][name][/RAW]][/text]
			[text]searchable=[J_getVar name=columns[[index]][RAW][searchable][/RAW]][/text]
			[Showif [validCols]^|[nameit]|]
				[Hideif [isNull varName=searchVal]=T]
					[Showif [searchable]=true]
						[text]sField=[lookup table=DNA_J_Mappings&value=[nameit]&lookinfield=ALIAS&returnfield=SEARCHFIELD&notfound=[nameit]][/text]
						[text]sVal=[sVal][Hideif [firstone]=f] OR[/Hideif] [sField] LIKE [URL]'%[CleanVar varName=searchVal]%'[/URL][/text]
						[text]firstone=t[/text]
					[/Showif]
				[/Hideif]
			[/Showif]
		[/Loop]
	[/Hideif]
	[RETURN][sVal][/RETURN]
[/Function]

[Function name=J_getOrderVar]
	[text]firstone=f[/text]
	[text]orderVar=[/text]
	[Hideif [isNull varName=orderCount]=T]
		[Loop start=0&end=[MATH][orderCount]-1[/MATH]]
			[text]orderCol=[J_getVar name=order[[index]][RAW][column][/RAW]][/text]
			[text]orderDir=[J_getVar name=order[[index]][RAW][dir][/RAW]][/text]
			[text]orderable=[J_getVar name=columns[[orderCol]][RAW][orderable][/RAW]][/text]
			[text]orderColName=[J_getVar name=columns[[orderCol]][RAW][name][/RAW]][/text]
			[Showif [validCols]^|[orderColName]|]
				[Showif [orderable]=true]
					[text]oField=[lookup table=DNA_J_Mappings&value=[orderColName]&lookinfield=ALIAS&returnfield=ORDERFIELD&notfound=[orderColName]][/text]
					[text]orderVar=[orderVar][Hideif [firstone]=f],[/Hideif][oField] [orderDir][/text]
					[text]firstone=t[/text]
				[/Showif]
			[/Showif]
		[/Loop]
	[/Hideif]
	[RETURN][orderVar][/RETURN]
[/Function]